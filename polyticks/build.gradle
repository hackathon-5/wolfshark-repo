
buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			url "http://oss.sonatype.org/content/repositories/snapshots/"
		}
		flatDir {
			dirs "lib/buildscript"
		}
	}

	dependencies {
                classpath name: "gradle-twirl-custom"
                classpath "org.scala-lang:scala-library:2.11.2"
                classpath "com.typesafe.play:twirl-compiler_2.11:1.0.2"
                classpath "com.typesafe.play:twirl-api_2.11:1.0.2"
                classpath "org.apache.maven:maven-ant-tasks:2.1.3"

                classpath "com.github.marceloemanoel:gradle-migrations:0.2.1"
                classpath "de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3"
                classpath ("nu.studer:gradle-jooq-plugin:1.0.5") { exclude group: "org.jooq" }
				classpath "org.jooq:jooq:${jooqVersion}"
				classpath "org.jooq:jooq-meta:${jooqVersion}"
				classpath "org.jooq:jooq-codegen:${jooqVersion}"
                classpath "org.postgresql:postgresql:9.3-1102-jdbc41"
                classpath "org.akhikhl.gretty:gretty:+"
        }
}

apply plugin: "java"
apply plugin: "scala"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "twirl"
apply plugin: "migrations"
apply plugin: "lesscss"
apply plugin: "nu.studer.jooq"
apply plugin: "org.akhikhl.gretty"

version = "0.0.1"

war {
	archiveName "ROOT.war"
}

// Setup Scala joint-compilation for Twirl templates
// See: https://github.com/67726e/gradle-twirl
sourceSets {
	// See: http://forums.gradle.org/gradle/topics/how_to_compile_a_java_class_that_depends_on_a_scala_class_in_gradle
	main {
		scala {
			srcDir "src/main/java"
		}

		java {
			srcDirs = []
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url "http://oss.sonatype.org/content/repositories/snapshots/"
	}
	flatDir {
		dirs "lib"
	}
}

dependencies {
	// Spring
	compile "org.springframework:spring-core:${springVersion}"
	compile "org.springframework:spring-web:${springVersion}"
	compile "org.springframework:spring-webmvc:${springVersion}"
	compile "org.springframework:spring-tx:${springVersion}"
	compile "org.springframework:spring-orm:${springVersion}"
	compile "org.springframework:spring-oxm:${springVersion}"

	// jOOQ
	compile "org.postgresql:postgresql:9.3-1102-jdbc41"
	compile "org.jooq:jooq:${jooqVersion}"
	compile "org.apache.commons:commons-dbcp2:2.0"

	// Scala & Twirl
	compile "org.scala-lang:scala-library:${scalaVersion}"
	compile "us.hexcoder:spring-mvc-twirl_2.11:1.1.0-SNAPSHOT"

	// JSR-303 Validations
	compile "javax.validation:validation-api:1.1.0.Final"
	compile "org.hibernate:hibernate-validator:5.1.3.Final"

	// Misc. Libraries
	compile "com.google.guava:guava:18.0"
	compile "com.google.code.gson:gson:2.3.1"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.3.2"
	compile "org.jasypt:jasypt:1.9.2"
	compile "org.jasypt:jasypt-spring31:1.9.2"

	// Container provided libraries
	providedCompile "javax.servlet:javax.servlet-api:3.1.0"

	// Migrations
	migrationsDriver "org.postgresql:postgresql:9.3-1102-jdbc41"
}

// See: https://github.com/etiennestuder/gradle-jooq-plugin
jooq {
	polyticks(sourceSets.main) {
		jdbc {
			driver = "org.postgresql.Driver"
			url = "jdbc:postgresql://localhost/polyticks"
			user = "polyticks"
			password = "polyticks"
		}

		generator {
			database {
				name = "org.jooq.util.postgres.PostgresDatabase"
				inputSchema = "public"
			}

			target {
				packageName = "us.hexcoder.polyticks.jooq"
			}
		}
	}
}

// See: https://github.com/obecker/gradle-lesscss-plugin
lesscss {
	source = fileTree("src/main/less") {
		include "*.less"
	}

	dest = "src/main/webapp/css"
	compress = true
}

war { dependsOn "lesscss" }

gretty {
	contextPath = "/"
	jvmArgs = ["-Dorg.eclipse.jetty.annotations.maxWait=120"]
}

